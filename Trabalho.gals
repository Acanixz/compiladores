#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = true
ScannerTable = Full
Input = String
Parser = SLR
#RegularDefinitions

#Tokens
// Comentários:
// - Declaração para o vetor na gramática (declaracao_vetor e proximas 2 linhas) não parece funcionar corretamente no Web GALS, algum bug com os simbolos GREATER and LESS quando fecham

// COMENTÁRIOS
MULTI_LINE_COMMENT: /\*([^\*]|[\r\n]|(\*+([^\*/]|[\r\n])))*\*/
ONE_LINE_COMMENT: [/][/][^\n]*

// PALAVRAS RESERVADAS
// Não podem ser usadas p/ variaveis
VECTOR_KEYWORD: "vector"
BOOL_KEYWORD: "bool"
VOID_KEYWORD: "void" // Nota: Não usado em literais
INT_KEYWORD: "int"
FLOAT_KEYWORD: "float"
DOUBLE_KEYWORD: "double" // Nota: Não usado em literais
CHAR_KEYWORD: "char"
STRING_KEYWORD: "string"
IF_KEYWORD: "if"
ELSE_KEYWORD: "else"
WHILE_KEYWORD: "while"
FOR_KEYWORD: "for"
RETURN_KEYWORD: "return"
CIN_KEYWORD: "cin"
COUT_KEYWORD: "cout"

// COMANDOS UTILITÁRIOS
ENDL_KEYWORD: "endl"
RETURN_KEYWORD: "return"

// COMANDOS DA BIBLIOTECA VECTOR
PUSH_BACK_KEYWORD: "push_back"
SIZE_KEYWORD: "size"

// LITERAIS
// O tipo de dado, definido pelas regras
INT_LITERAL: 0[bB][01]+ | 0[xX][0-9a-fA-F]+ | [0-9]+
FLOAT_LITERAL: [0-9]+\.[0-9]* | \.[0-9]+[fF]?
CHAR_LITERAL: '([^'\\]|\\[\\'\"tnr])'
STRING_LITERAL: \"([^\"\\]|\\.)*\\?\"
BOOL_LITERAL: true | false
NULL_LITERAL: nullptr

// OPERADORES ARITMÉTICOS
PLUS: "+"
MINUS: "-"
TIMES: "*"
DIVIDE: /
MODULO: %
INCREMENT: "++"
DECREMENT: "--"

// ATRIBUIÇÃO
EQUAL: =

// OPERADORES RELACIONAIS E ATRIBUIÇÃO
EQUALS: ==
NOT_EQUALS: !=
GREATER: >
LESS: <
GREATER_OR_EQUALS: >=
LESS_OR_EQUALS: <=

// OPERADORES LÓGICOS
LOGICAL_AND: &&
LOGICAL_OR: "||"
LOGICAL_NOT: !

// OPERADORES BIT A BIT
BITWISE_AND: &
BITWISE_OR: "|"
BITWISE_XOR: "^"
BITWISE_NOT: ~
LEFT_SHIFT: <<
RIGHT_SHIFT: >>

// IDENTIFICADOR
IDENTIFIER: [a-zA-Z_]?[a-zA-Z0-9_]*[a-zA-Z0-9]

// DELIMITADORES E PONTUADORES
DOT: "."
SEMICOLON: ;
COLON: :
COMMA: ,
LEFT_PARENTHESIS: "("
RIGHT_PARENTHESIS: ")"
LEFT_BRACKET: "["
RIGHT_BRACKET: "]"
LEFT_BRACE: "{"
RIGHT_BRACE: "}"

// Ignore
// Espaço, Whiteline
:[\ \n]
#NonTerminals
<lista_comandos>
<keyword_tipo>
<keyword_literal>
<dado>
<exp>
<exp1>
<exp2>
<exp3>
<exp4>
<exp5>
<exp6>
<exp7>
<exp8>
<exp9>
<exp10>
<op_rel>
<op_neg>
<op_bitwise>
<op_arit_baixa>
<op_arit_alta>
<comando>
<lista_comandos_escopo>
<declaracao_variavel>
<declaracao_var_id>
<declaracao_var_value>
<variavel1>
<declaracao_vetor>
<vetor1>
<vetor2>
<vetor3>
<nome_vetor>
<argumento>
<chamada_push_back_vetor>
<chamada_size_vetor>
<posicao_vetor>
<posicao1>
<chamada_cin>
<chamada_cout>
<cout1>
<cout2>
<endl>
<chamada_if>
<chamada_else_if>
<chamada_else>
<if1>
<chamada_return>
<return1>
#Grammar
<keyword_tipo> ::= BOOL_KEYWORD | INT_KEYWORD | FLOAT_KEYWORD | DOUBLE_KEYWORD | CHAR_KEYWORD | STRING_KEYWORD;
<keyword_literal> ::= BOOL_LITERAL | INT_LITERAL | FLOAT_LITERAL| CHAR_LITERAL | STRING_LITERAL;
<dado> ::= IDENTIFIER | <keyword_literal>;


<exp>  ::= <exp> LOGICAL_OR <exp1> | <exp1>;
<exp1> ::= <exp1> LOGICAL_AND <exp2> | <exp2>;
<exp2> ::= <exp2> BITWISE_OR <exp3> | <exp3>;
<exp3> ::= <exp3> BITWISE_XOR <exp4> | <exp4>;
<exp4> ::= <exp4> BITWISE_AND <exp5> | <exp5>;
<exp5> ::= <exp5> <op_rel> <exp6> | <exp6>;
<exp6> ::= <exp6> <op_bitwise> <exp7> | <exp7>;
<exp7> ::= <exp7> <op_arit_baixa> <exp8> | <exp8>;
<exp8> ::= <exp8> <op_arit_alta> <exp9> | <exp9> ;
<exp9> ::= <op_neg> <exp10> | <exp10>;
<exp10> ::= INT_LITERAL | FLOAT_LITERAL | IDENTIFIER | LEFT_PARENTHESIS <exp> RIGHT_PARENTHESIS;

<op_rel> ::= GREATER | LESS | GREATER_OR_EQUALS | LESS_OR_EQUALS | EQUALS | NOT_EQUALS; 
<op_neg> ::= MINUS | BITWISE_NOT | LOGICAL_NOT;
<op_bitwise> ::= LEFT_SHIFT | RIGHT_SHIFT;
<op_arit_baixa> ::= PLUS | MINUS;
<op_arit_alta> ::= TIMES | DIVIDE | MODULO;


<comando> ::= <declaracao_variavel> | <declaracao_vetor> | <chamada_cin> | <chamada_cout> | <chamada_if> | <chamada_push_back_vetor> | <chamada_size_vetor> | <posicao_vetor> | <chamada_return>;
<lista_comandos> ::= <lista_comandos><comando> | <comando> | <lista_comandos_escopo>;
<lista_comandos_escopo> ::= LEFT_BRACE <comando> RIGHT_BRACE | LEFT_BRACE <lista_comandos><comando> RIGHT_BRACE;


<declaracao_variavel> ::= <keyword_tipo> <declaracao_var_id> <variavel1> SEMICOLON;
<declaracao_var_id> ::= IDENTIFIER | IDENTIFIER COMMA <declaracao_var_id>;
<declaracao_var_value> ::= <exp> | <exp> COMMA <declaracao_var_value>;
<variavel1> ::= î | EQUAL <declaracao_var_value>;


<declaracao_vetor> ::= VECTOR_KEYWORD LESS <keyword_tipo> GREATER IDENTIFIER <vetor1> SEMICOLON;
<vetor1> ::= î | EQUAL LEFT_BRACE <vetor3> RIGHT_BRACE | LEFT_PARENTHESIS <vetor2> RIGHT_PARENTHESIS;
<vetor2> ::= <dado> | <dado> COMMA <dado>;
<vetor3> ::= <vetor2> | <vetor2><vetor3>;

<nome_vetor> ::= IDENTIFIER DOT;
<argumento> ::= LEFT_PARENTHESIS <dado> RIGHT_PARENTHESIS;

<chamada_push_back_vetor> ::= <nome_vetor> PUSH_BACK_KEYWORD <argumento> SEMICOLON;
<chamada_size_vetor> ::= <nome_vetor> SIZE_KEYWORD <argumento> SEMICOLON;
<posicao_vetor> ::= IDENTIFIER LEFT_BRACKET <posicao1> RIGHT_BRACKET SEMICOLON;
<posicao1> ::= <dado> | <exp>;

<chamada_cin> ::= CIN_KEYWORD RIGHT_SHIFT IDENTIFIER SEMICOLON;


<chamada_cout> ::= COUT_KEYWORD LEFT_SHIFT <cout1> SEMICOLON;
<cout1> ::= <cout2> | <cout2> LEFT_SHIFT <cout1>;
<cout2> ::= <exp> | <keyword_literal> | <endl>;
<endl> ::= ENDL_KEYWORD;


<chamada_if> ::= IF_KEYWORD LEFT_PARENTHESIS <exp> RIGHT_PARENTHESIS <lista_comandos_escopo> <if1>;
<chamada_else_if> ::= ELSE_KEYWORD IF_KEYWORD LEFT_PARENTHESIS <exp> RIGHT_PARENTHESIS <lista_comandos_escopo> <if1>;
<chamada_else> ::= ELSE_KEYWORD <lista_comandos_escopo>;
<if1> ::= î | <chamada_else_if> | <chamada_else>;


<chamada_return> ::= RETURN_KEYWORD <return1> SEMICOLON;
<return1> ::= î | <dado> | <exp>;